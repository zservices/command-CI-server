#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

# Bootstrap complete exec environment.
builtin emulate -L zsh
0=${${0:#$ZSH_ARGZERO}:-${(%):-%N}}
0=${${(M)0##/*}:-$PWD/${0#./}}
source $0:h:h/lib/common.snip.zsh cmd-run $0:h:h

local -a opt_help 
csrv-edit-parse-opts() {
    builtin zparseopts -D -E -- \
            -help=opt_help h=opt_help
    reply=("$@")
}

csrv-edit-usage() {
    msg The \`{40}edit%f\` subcommand runs the \$EDITOR on \
        command definition file found in {39}\$CMD_COMMAND_PATH%f \
        or on file found by a relative or absolute path.
    msg %B{39}Usage:%b%f
    msg \\t%B{40}cmd%b edit {39}"[options…]"%f \\x7bcommand-ID\\x7d
    msg "{39}[-h|--help]"%f\\t\ \ –\\t This message
}

csrv-edit-parse-opts "$@"
set -- "$reply[@]"
(($#opt_help)) && {csrv-edit-usage; return 0;}
local cmd=$1 cmd_name=$1:t:r
integer exit_code=1

csrv-check-file "$cmd" command || return $?
cmd=$REPLY

# Read command definition.
@str-read-toml $cmd INI

# Replace any special sequences like %var or $var.
csrv-replace-vars INI

#
# Run cmd catching its output.
#

# The local out file.
outfile=$cmd:h/_$cmd_name.out
# x_* keys are extra, not from file.
INI[x_ini_path]=$cmd

csrv-get-editor || return $?

# The main line of code.
repeat 1 { $REPLY "$cmd"; exit_code=$?; }

return exit_code

# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
