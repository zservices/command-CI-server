#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

local q qout=${${1%%/##}:-/} it=$2 TMP

# Get a working, existing path to any folder given â†” $qout or upper $qout/../
if ! csrv-norm-path $qout; then
    msg {208}Error:%f Cannot determine output file \(for the TAGS/tags \
                index\). Try adapting {41}-o$f/{41}--out%f option, or skip it, \
                to use the default \({39}./TAGS%f or {39}./tags%f\)
fi

q=$REPLY

# Append TAGS/tags file name ?
((it))&&TMP=tags||TMP=TAGS
if [[ -d $q ]]; then
    q+=/$TMP
    # Attempt of updating the given, raw -o/--out path,
    # with a TAGS/tags file name.
fi

# Append file name only, if not specified manually in the option.
[[ -d $qout ]]&&qout=${${(M)qout:#(#i)(#m)*/TAGS}:-$qout/$TMP}

# Make path absolute.
[[ $qout != /* ]] && qout=$PWD/${qout#./}

local _qout=${qout/$PWD/.} _q=${q/$PWD/.}
[[ $_q == /# ]] && {_q=$PWD:t/$TMP; q=$PWD/$TMP;}
[[ $_qout == /# ]] && { _qout=$PWD:t/$TMP; qout=$PWD/$TMP;}

# Inform user that his path has been changed.
if [[ $_qout != $_q  ]]; then
    msg {208}Warning:%f {39}out file/dir path%f \({70}-o%f/{70}--out%f=\
{69}$_qout%f\) was incorrect, changed it to %B{40}$_q%f%b.
fi

REPLY=$q

[[ -n $REPLY ]]

# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
