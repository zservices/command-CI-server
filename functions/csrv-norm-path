#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

#
# Make path regular.
#

local qout=$1 inq=$1 q
integer verbose=$2

# No out path provided? → $PWD.
csnon_empty $qout || qout=$PWD
# Remove trailing /.
qout=${${qout%%/##}:-/tmp/WARN_ROOT_PATH_DETECT}
# Fix relative paths.
[[ $qout != /* ]] && qout=$PWD/$qout
# Remoe ./ from path.
qout=${qout//.\/##/}
# Remove //+ series from path.
qout=${qout//\/##//}

# Remove dir/.. component(s).
while [[ $qout != /## && $qout == *[^/]##/../* ]]; do
    qout=${qout/((\/|(#s))[^\/]##\/)(..)/}
done

[[ -f $qout || -d $qout ]] && q=$qout
[[ ! -e $qout && -d $qout:h && $qout != /# ]] && q=$qout

if [[ ! -e $q ]]; then
    [[ -w $q:h && -x $q:h ]] || q=
elif [[ -f $q ]]; then
    [[ -w $q:h && -x $q:h && -w $q ]] || q=
elif [[ -d $q ]]; then
    [[ -w $q && -x $q ]] || q=
fi

# Traverse qout up, searching for existing dir.
if [[ -z $q ]]; then
    while [[ (! -d $qout || ! -w $qout || ! -x $qout) && $qout != /# ]]; do
        ((verbose))&&msg {27}Trying output file path: {41}$qout{79}… %B{208}fail
        qout=$qout:h
    done
    # Found non root path? ↔ /something and more levels.
    [[ $qout == /[^/]##* ]]&&q=$qout
    ((verbose))&&msg %B{79}Found path:{39} «{41}$qout{39}»{79}. It is: \
        {203}%B${${q:+usable}:-unusable}
fi

REPLY=$q

# Return true if something changed.
[[ -n $REPLY && ((-d $q && -w $q && -x $q) || \
                   ( -f $q && -w $q && -x $q:h) || \
                      (! -e $q && -d $q:h && -w $q:h && -x $q:h)) ]]

# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
