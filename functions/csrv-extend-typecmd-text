#!/usr/bin/env zsh
csrv-extend-typecmd-text() {
    local var_name=$1 cmd=$2 tpe sh
    shift 2

    local -a args=( "$@" )
    local -A Config
    Config=( "${(@PAkv)var_name}" )

    local prefix="$Config[main__shell] -c"

    reply=()
    tpe=$Config[main__type]
    sh=$Config[main__shell]

    if [[ $tpe == source ]]; then 
        if [[ $sh != (none|) ]]; then
            # The arguments fall from positional params set for zsh -c
            # to the source command, which automatically inherits them.
            reply=( $sh -c "source\\ ${(q)cmd}" csrv-source )
        else
            # The positional parameters are automatically appended by
            # source builtin by the wrapping eval.
            reply=( "source ${(q)cmd}" )
        fi
    elif [[ $tpe == binary ]]; then
        if [[ $sh != (none|) ]]; then
            reply=( $sh -c '"'"$cmd"' \"\$@\""' csrv-bin )
        else
            reply=( "$cmd \"\$@\"" )
        fi
    elif [[ $tpe == autoload ]]; then
        if [[ $sh != (none|) ]]; then
            reply=( $sh -c "\"autoload $cmd; $cmd \\\"\\\$@\\\"\"" csrv-auto )
        else
            autoload $cmd
            reply=( $cmd )
        fi
    elif [[ $tpe == eval ]]; then
        if [[ $sh != (none|) ]]; then
            reply=( $sh -c '"'"eval $cmd"' \"\$@\""' csrv-eval )
        else
            reply=( eval $cmd )
        fi
    fi
    reply+=( "$args[@]" )
}

# vim:ft=zsh:sw=4:sts=4:ts=8:et
