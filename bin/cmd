#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

# Bootstrap complete exec environment.
builtin emulate -L zsh
0=${${0:#$ZSH_ARGZERO}:-${(%):-%N}}
0=${${(M)0##/*}:-$PWD/${0#./}}
source $0:h:h/lib/common.snip.zsh cmd $0:h:h

local subcmd=$1
shift
warn() {
    msg {208}Error: no such subcommand \`{39}$subcmd{208}\` found
}

case $subcmd in
    (*)
        if (( $+commands[cmd-$subcmd] )); then
            command cmd-$subcmd "$@"
        elif [[ -d $CMD_DIR ]]; then
            local libexec=$CMD_DIR/libexec
            if [[ -x $libexec/cmd-$subcmd ]]; then
                command $libexec/cmd-$subcmd "$@"
            else
                local -TU CMD_COMMAND_PATH q
                local -a arr
                arr=( $^q/*.(ini|cmd)(N:t:r) )
                arr=( ${(M)arr:#$subcmd} )
                if (( $#arr )); then
                    command $libexec/cmd-run $subcmd "$@"
                else
                    warn
                    return 1
                fi
            fi
        else
            warn
            return 3
        fi
        ;;
esac
# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
